apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

group = 'org.typesense'
version = '0.0.3'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes "Main-Class": "org.typesense.api.Client"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }

}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests (assumes running Docker image or local server)'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    shouldRunAfter test
}

check.dependsOn integrationTest

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation "org.junit.jupiter:junit-jupiter:5.7.0"
    implementation "io.swagger.core.v3:swagger-annotations:2.1.6"
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.13.3'
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    integrationTestImplementation 'junit:junit:4.13.2'
    integrationTestImplementation "org.hamcrest:hamcrest-all:1.3"
}
